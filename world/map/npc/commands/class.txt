-|script|@class|32767
{
    callfunc "argv_splitter";
    set .@n$, if_then_else(@argv$[1] != "", "char", "") + "class";
    if (GM < CMD_CLASS && GM < G_SYSOP) goto L_GM; // check if you can use it on self
    set .@target_id, BL_ID;
    if (@argv$[1] != "") set .@target_id, getcharid(3, @argv$[1]);
    if (@argv$[1] != "" && !(isloggedin(.@target_id))) goto L_Failed; // do NOT fallback to self
    if (@argv$[1] != "" && GM < CMD_CHARCLASS && GM < G_SYSOP) goto L_GM; // when target is not self, use charclass permission

    set .@c, 1; // default to human
    if (@argv[0] >= 1 || @argv[0] <= 32767) set .@c, @argv[0];
    set Class, .@c, .@target_id;
    gmlog "@"+.@n$+" " + @args$;
    message strcharinfo(0), .@n$+" : The operation succeeded.";
    end;

L_Failed:
    // XXX: should we allow GMs to change class of users that are not logged in?
    message strcharinfo(0), .@n$+" : Impossible to attach to the target player. Did you try putting the name in \"quotation marks\"?";
    end;

L_GM:
    message strcharinfo(0), .@n$+" : GM command is level "+ if_then_else(@argv$[1] != "", CMD_CHARCLASS, CMD_CLASS) +", but you are level " + GM;
    end;

OnInit:
    registercmd chr(ATCMD_SYMBOL) + "class", strnpcinfo(0);
    registercmd chr(ATCMD_SYMBOL) + "charclass", strnpcinfo(0);
    end;
}
