-|script|@changesex|32767
{
    callfunc "argv_splitter";
    set .@n$, if_then_else(@argv$[1] != "", "char", "") + "changesex";
    if (GM < CMD_CHANGESEX && GM < G_SYSOP) goto L_GM; // check if you can use it on self
    set .@target_id, BL_ID;
    if (@argv$[1] != "") set .@target_id, getcharid(3, @argv$[1]);
    if (@argv$[1] != "" && !(isloggedin(.@target_id))) goto L_Failed; // do NOT fallback to self
    if (@argv$[1] != "" && GM < CMD_CHARCHANGESEX && GM < G_SYSOP) goto L_GM; // when target is not self, use charchangesex permission

    set .@s, 3; // default to non-binary
    if (@argv$[0] == "M" || @argv$[0] == "m") set .@s, 1;
    if (@argv$[0] == "F" || @argv$[0] == "f") set .@s, 0;
    set Sex, .@s, .@target_id;
    gmlog "@"+.@n$+" " + @args$;
    message strcharinfo(0), .@n$+" : The operation succeeded.";
    end;

L_Failed:
    // XXX: should we allow GMs to change sex of users that are not logged in?
    message strcharinfo(0), .@n$+" : Impossible to attach to the target player. Did you try putting the name in \"quotation marks\"?";
    end;

L_GM:
    message strcharinfo(0), .@n$+" : GM command is level "+ if_then_else(@argv$[1] != "", CMD_CHARCHANGESEX, CMD_CHANGESEX) +", but you are level " + GM;
    end;

OnInit:
    registercmd chr(ATCMD_SYMBOL) + "changesex", strnpcinfo(0);
    registercmd chr(ATCMD_SYMBOL) + "charchangesex", strnpcinfo(0);
    end;
}
