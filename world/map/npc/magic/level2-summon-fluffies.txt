-|script|smfluffies|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 39) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("WhiteFur") < 1 || countitem("Root") < 1) end;
    delitem "WhiteFur", 1;
    delitem "Root", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 20000, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 39;
    misceffect FX_MAGIC_BLUE, strcharinfo(0);
    misceffect FX_PENTAGRAM_BUILDUP, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/170)+1+(@spellpower/430), .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*350, .@puppet;
    addnpctimer 5000-(@spellpower*8), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_PENTAGRAM_BURST;
    set .@i, 0;
    set .@x, getnpcx();
    set .@y, getnpcy();
    set .@map$, strnpcinfo(3);
    callsub S_SummonAll;
    end;

OnDestroy:
    destroy;

S_SummonAll:
    summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, 1020, 2, .lifetime;
    set .@i, .@i + 1;
    if (.@i < .count) goto S_SummonAll;
    destroy;

OnInit:
    set .school, SKILL_MAGIC_ASTRAL;
    set .invocation$, chr(MAGIC_SYMBOL) + "kalakarenk"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 1;
    end;
}
